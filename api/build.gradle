import com.github.retrooper.MappingCompressionTask

// papermc repo + disableAutoTargetJvm needed for mockbukkit
repositories {
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}


javadoc {
    destinationDir = file("${buildDir}/docs/javadoc")
}

java {
    disableAutoTargetJvm()
    sourceSets {
        main {
            resources {
                srcDirs += ["$buildDir.canonicalPath/mappings/generated"]
            }
        }

        test {
            resources {
                srcDirs += ["$buildDir.canonicalPath/mappings/generated"]
            }
        }
    }
}

dependencies {
    compileOnly(adventureDependencies)

    testImplementation(adventureDependencies)
    testImplementation(project(":netty-common"))
    testImplementation("com.github.seeseemelk:MockBukkit-v1.20:3.9.0")
    testImplementation("org.slf4j:slf4j-simple:2.0.7")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.0")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.10.0")
}

test {
    useJUnitPlatform()
}

task generateMapping(type: MappingCompressionTask) {
    mappingsDir = rootDir.toPath().resolve("mappings")
    outDir = project.buildDir.toPath().resolve("mappings/generated/assets/mappings")
    outputs.upToDateWhen {
        def gen_modified = project.buildDir.toPath().resolve("mappings/generated/assets/mappings").toFile().lastModified()
        def list = []
        project.rootProject.projectDir.toPath().resolve("mappings").toFile().eachFileRecurse(groovy.io.FileType.FILES) { file ->
            list << file
        }

        for (File file : list) {
            if (file.lastModified() > gen_modified) {
                return false
            }
        }

        return true
    }
}

tasks.processResources.dependsOn(generateMapping)